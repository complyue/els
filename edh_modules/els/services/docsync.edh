
import ( world, elsDebug, **_ ) 'els'

import * './utils'


export method @'textDocument/didOpen' ( textDocument, ***_ ) void
# try parse & resolve then publish the diagnostics asynchronously,
# to not block subsequent requests from the lsp client
case textDocument of { TxtDocFile ( absFilePath ) } -> { go {
    moduOpen = world.locateByFile( absFilePath )
    perform lspNotify( 'textDocument/publishDiagnostics',
      uri= textDocument[ 'uri' ],
      version= textDocument[ 'version' ],
      diagnostics= world.diags( moduOpen ),
    )
} }

export method @'textDocument/didClose' ( textDocument, ***_ )
pass # notification, don't mandate a response


export method @'textDocument/didChange' (
  textDocument, contentChanges, ***_
) void {
  case contentChanges of {
    { chgContent :> moreChgs } | null( moreChgs )
    -> case chgContent[ 'text' ] of {
      { fullTxt } | type( fullTxt ) is StringType
      -> { pass }
      error( 'no text in the sole change' )
    }

    # only Full change is supported by far
    error( 'too many changes' )
  }

  case textDocument of { TxtDocFile ( absFilePath, version= docVersion ) } -> {
    moduChgd = world.locateByFile( absFilePath )
    # the parsing after invalidated, will pend starting until otf source is
    # seen stabilized, i.e. no further otf change after 350 ms.
    #
    # the first otf filler is responsible to report back diags, `fill()` tells
    # whether this is the first fill since last parsed.
    if moduChgd.fill( docVersion, fullTxt, ) then go {
      # obtain diags by resolving (which triggers parsing in turn),
      # report the result back to lsp client
      perform lspNotify( 'textDocument/publishDiagnostics',
        uri= textDocument[ 'uri' ],
        version= textDocument[ 'version' ],
        diagnostics= world.diags( moduChgd ),
      )
    }
  }
}


export method @'textDocument/didSave' ( textDocument, ***_ )
pass # notification, don't mandate a response
