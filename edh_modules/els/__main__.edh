{##
 # Entry module of Đ (Edh) Language Server
 #}

import ( LangServer, sendTextToFd, ** ) 'els/RT'
import ( edhLanguageService, ** ) './service'


# it's customizable with a nested `els/config.edh`, in `edh_modules` resides in cwd or any upper directory
import ( **cfg ) 'els/config'


# start listening
let els = LangServer( edhLanguageService, **cfg )

# wait the result, either sucess or failure
case els.addrs() of {
  { elsAddr :>_ } -> console.info
  <| 'Đ (Edh) Language Server listening: ' ++ elsAddr
  # or the network has failed, propagate the error
  els.join() # this usually throws
  # in case join() didn't throw, report this error
  error( 'Đ (Edh) Language Server failed listening.' )
}

# success, assuming launched by lsc extension, try report actual port to fd 3
{
  bytesWritten = sendTextToFd( 3, str( elsAddr.port() ) )
  console.debug<| 'Reported els port ' ++ elsAddr.port() ++ ' to fd 3 ('
  ++ bytesWritten ++ ' bytes)'
} $=> { exc } -> {
  console.warn<| 'Failed reporting els port ' ++ elsAddr.port() ++ ' to fd 3'
}

# keep the server process running
if debug then {
  import * 'els/debug'

  # show session count and url list every 5 minutes
  do elsDebug.logSessions()
  for _ from console.everySeconds( 300 )

} else {
  els.join()
}
